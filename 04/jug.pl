% Две бутылки: на 3 и 5 литров. Используя только их, налить 4 литра. 

% s(X,Y) -- состояние: в первой бутылке X литров (<= 3), во второй -- 5.
pour(s(_, Y), s(0, Y)). % Выливаем воду из первой бутылки
pour(s(X, _), s(X, 0)). % Выливаем воду из второй бутылки
pour(s(0, Y), s(3, Y)). % Наливаем воду в первую бутылку
pour(s(X, 0), s(X, 5)). % Наливаем воду во вторую бутылку

pour(s(X, Y), s(Z, 5)) :- % Переливаем воду из первой бутылки во вторую до упора
    Z is X - (5 - Y), Z >= 0.
pour(s(X, Y), s(Z, 0)) :- % Переливаем всю воду из второй бутылки в первую
    Z is X + Y, Z =< 3. 
pour(s(X, Y), s(3, Z)) :- % Переливаем воду из второй бутылки в первую до упора
    Z is Y - (3 - X), Z >= 0.
pour(s(X, Y), s(0, Z)) :- % Переливаем всю воду из первой бутылки во вторую
    Z is X + Y, Z =< 5. 

solve(Xs) :- solve([s(0,0)], Xs). 
solve([s(X0, Y0) | T], [s(X1, 4), s(X0, Y0)| T]) :- 
    pour(s(X0, Y0), s(X1, 4)), 
    !.
solve([s(X0, Y0) | T], Xs) :- 
    pour(s(X0, Y0), s(X1, Y1)), 
    not(member(s(X1,Y1),[s(X0,Y0)|T])),
    solve([s(X1, Y1), s(X0, Y0) | T], Xs). 

